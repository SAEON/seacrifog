prefix cpres: <http://meta.icos-cp.eu/resources/cpmeta/>
prefix cpmeta: <http://meta.icos-cp.eu/ontologies/cpmeta/>
prefix prov: <http://www.w3.org/ns/prov#>

select
?dobj
?fileName
?variable
?stationName
?height
?timeStart
?timeEnd
?stationId

where {
    values ?vtype {
        cpres:co2MixingRatio
        cpres:coMixingRatioPpb
        cpres:ch4MixingRatioPpb
    }

    values ?spec {
        cpres:atcCo2NrtGrowingDataObject
        cpres:atcCoNrtGrowingDataObject
        cpres:atcCh4NrtGrowingDataObject
    }
    
    ?vtype rdfs:label ?variable .
    ?col cpmeta:hasValueType ?vtype .
    ?dset cpmeta:hasColumn ?col .
    ?spec cpmeta:containsDataset ?dset .
    ?spec cpmeta:hasAssociatedProject <http://meta.icos-cp.eu/resources/projects/icos> .
    ?spec cpmeta:hasDataLevel "1"^^xsd:integer .
    ?dobj cpmeta:hasObjectSpec ?spec .
    ?dobj cpmeta:hasName ?fileName .
    ?dobj cpmeta:hasSizeInBytes ?fileSize .
    filter not exists {
        [] cpmeta:isNextVersionOf ?dobj
    }
    ?dobj cpmeta:wasAcquiredBy [
        prov:wasAssociatedWith/cpmeta:hasStationId ?stationId ;
        prov:startedAtTime ?timeStart ;
        prov:endedAtTime ?timeEnd ;
        prov:wasAssociatedWith/cpmeta:hasName ?stationName ;
        cpmeta:hasSamplingHeight ?height
    ]
}

order by
?variable
?stationName
?height